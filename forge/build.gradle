plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id "com.modrinth.minotaur" version "2.+"
}

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath

    forge {
        convertAccessWideners = true
        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name

        mixinConfig "template-common.mixins.json"
        mixinConfig "template.mixins.json"
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin since it *excludes* files.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.forge_version}"
    // Remove the next line if you don't want to depend on the API
    modApi "dev.architectury:architectury-forge:${rootProject.architectury_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }

    modImplementation "curse.maven:supermartijn642s-config-lib-438332:${project.config_library_file_forge}"
    modImplementation "curse.maven:ui-933200:${project.ui_library_file_forge}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mod_id", project.mod_id
    inputs.property "mod_name", project.mod_name
    inputs.property "mod_description", project.mod_description
    inputs.property "mod_author", project.mod_author
    inputs.property "mod_license", project.mod_license
    inputs.property "mod_repository", project.mod_repository

    inputs.property "forge_loader_version", project.forge_loader_version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "architectury_version", project.architectury_version
    inputs.property "config_library_version", project.config_library_version
    inputs.property "ui_library_version", project.ui_library_version

    filesMatching("META-INF/mods.toml") {
        expand([
                "version": project.version,
                "mod_id": project.mod_id,
                "mod_name": project.mod_name,
                "mod_description": project.mod_description,
                "mod_author": project.mod_author,
                "mod_license": project.mod_license,
                "mod_repository": project.mod_repository,

                "forge_loader_version": project.forge_loader_version,
                "minecraft_version": project.minecraft_version,
                "architectury_version": project.architectury_version,
                "config_library_version": project.config_library_version,
                "ui_library_version": project.ui_library_version
        ])
    }

    filesMatching("pack.mcmeta") {
        expand "mod_name": project.mod_name
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "forge-dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = "forge"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

remapSourcesJar {
    archiveClassifier = "forge-sources"
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

curseforge {
    apiKey = System.getenv("CURSEFORGE_API_KEY")
    project {
        id = project.curse_forge_project_id
        releaseType = project.release_type
        changelogType = "markdown"
        changelog = rootProject.file('changelog.md')

        project.supported_minecraft_versions.split(',').each { version ->
            addGameVersion version
        }

        addGameVersion "Java 17"
        addGameVersion "Forge"

        relations {
            project.curseforge_dependencies.split(',').each { dep ->
                requiredDependency dep.trim()
            }
        }

        mainArtifact(remapJar) {
            displayName = "$project.mod_name Forge $project.minecraft_version - $project.mod_version"
        }

        addArtifact(remapSourcesJar) {
        }
    }
}

modrinth {
    token = System.getenv("MODRINTH_API_KEY")
    projectId = project.modrinth_project_id
    versionName = "$project.mod_name Forge $project.minecraft_version - $project.mod_version"
    versionNumber = "$project.mod_version"
    versionType = "$project.release_type"
    uploadFile = remapJar
    additionalFiles = [remapSourcesJar]
    gameVersions = project.supported_minecraft_versions.split(',').toList()
    loaders = ["forge"]
    changelog = rootProject.file('changelog.md').text
    dependencies {
        project.modrinth_dependencies.split(',').each { dep ->
            required.project dep
        }
    }
}